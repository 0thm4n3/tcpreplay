
dnl $Id: configure.in,v 1.19 2002/11/20 19:32:58 aturner Exp $

AC_INIT(tcpreplay.c)
AC_CONFIG_HEADER(config.h)

dnl Set version info here!
MAJOR_VERSION=1
MINOR_VERSION=3
MICRO_VERSION=0-beta6
TCPREPLAY_VERSION=$MAJOR_VERSION.$MINOR_VERSION.$MICRO_VERSION

dnl Release is only used for the RPM spec file
TCPREPLAY_RELEASE=1


AC_DEFINE_UNQUOTED(VERSION, "$TCPREPLAY_VERSION")
AC_SUBST(TCPREPLAY_VERSION)
AC_SUBST(TCPREPLAY_RELEASE)


dnl Initialize prefix.
if test "$prefix" = "NONE"; then
	prefix="/usr/local"
fi

dnl Determine OS
AC_CANONICAL_BUILD
AC_CANONICAL_HOST
AC_CANONICAL_TARGET
AC_SUBST(host)
AC_SUBST(build)
AC_SUBST(target)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_CPP
AC_FUNC_MEMCMP
AC_PATH_PROG(PRINTF, printf)
AC_SUBST(PRINTF)

AC_PATH_PROG(LIBNET_CONFIG, libnet-config)
AC_SUBST(LIBNET_CONFIG)

dnl Enable debugging in code/compiler options
debug=no
AC_MSG_CHECKING(for debug enabled)
AC_ARG_WITH(debug,
[  --with-debug            Enable debugging code and support for the -d option],
[debug=yes
 AC_DEFINE(DEBUG) 
 AC_MSG_RESULT(yes)], 
AC_MSG_RESULT(no))

dnl Use these compiler flags if we have gcc.
if test $ac_cv_prog_gcc = yes -a $debug = no ; then
    CCOPTS='-O2 -pipe -Wall'
    CFLAGS="$CCOPTS"
else
	CCOPTS='-pipe -Wall -ggdb'
	CFLAGS="$CCOPTS"
fi

dnl Check for types.
AC_CHECK_TYPE(u_int8_t, uint8_t)
AC_CHECK_TYPE(u_int16_t, uint16_t)
AC_CHECK_TYPE(u_int32_t, uint32_t)
AC_CHECK_TYPE(u_int64_t, uint64_t)

dnl Checks for libraries.
AC_CHECK_LIB(socket, socket)
AC_CHECK_LIB(nsl, gethostbyname)
AC_CHECK_LIB(m, fmod, , AC_MSG_ERROR(libm not found))
AC_FUNC_MEMCMP

dnl Checks for header files.
AC_HEADER_STDC
dnl AC_CHECK_HEADERS(err.h math.h sys/time.h signal.h)
AC_CHECK_HEADERS(math.h sys/time.h signal.h)


dnl use inet_aton or inet_addr ?
dnl AC_CHECK_DECL(inet_aton,
dnl 	AC_DEFINE(INET_ATON)
dnl 	inet_aton=yes, 
dnl 	inet_aton=no,
dnl 	[
dnl #include <sys/socket.h>
dnl #include <netinet/in.h>
dnl #include <arpa/inet.h>
dnl ])

dnl AC_CHECK_DECL(inet_addr,
dnl 	AC_DEFINE(INET_ADDR)
dnl 	inet_addr=yes, 
dnl 	inet_addr=no,
dnl 	[
dnl #include <sys/socket.h>
dnl #include <netinet/in.h>
dnl #include <arpa/inet.h>
dnl ])

AC_CHECK_FUNC(inet_aton, AC_DEFINE(INET_ATON) inet_aton=yes, inet_aton=no)
AC_CHECK_FUNC(inet_addr, AC_DEFINE(INET_ADDR) inet_addr=yes, inet_addr=no)

if test $inet_aton = no -a $inet_addr = no ; then
	AC_MSG_ERROR("We need either inet_aton or inet_addr")
fi
	
dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_SIZE_T
AC_HEADER_TIME

dnl Checks for library functions.
AC_CHECK_FUNCS(gettimeofday regcomp ctime)

dnl Check for err
dnl neederr=no
dnl AC_CHECK_FUNCS(warnx, , [neederr=yes])
dnl if test $neederr = yes; then
dnl	EXTRAS="$EXTRAS err.c"
dnl fi
AC_SUBST(EXTRAS)

dnl Checks for libnet (shamelessly horked from dsniff)
AC_MSG_CHECKING(for libnet)
AC_ARG_WITH(libnet,
[  --with-libnet=DIR       use libnet in DIR],
[ case "$withval" in
  yes|no)
     AC_MSG_RESULT(no)
     ;;
  *)
     AC_MSG_RESULT($withval)
     if test -f $withval/include/libnet.h -a -f $withval/lib/libnet.a; then
        owd=`pwd`
        if cd $withval; then withval=`pwd`; cd $owd; fi
	if test -x $withval/bin/libnet-config; then
	   CFLAGS="$CFLAGS `$withval/bin/libnet-config --defines`"
	elif test -x $withval/libnet-config; then
	   CFLAGS="$CFLAGS `$withval/libnet-config --defines`"
	elif test -x $LIBNET_CONFIG; then
	   CFLAGS="$CFLAGS `$LIBNET_CONFIG --defines`"
	fi
	LNETINC="-I$withval/include"
	LNETINCDIR="$withval/include/"
	LNETLIB="-L$withval/lib -lnet"
     else
        AC_ERROR(libnet.h or libnet.a not found in $withval)
     fi
     ;;
  esac ],
[ if test -f ${prefix}/include/libnet.h; then
     if test -x ${prefix}/bin/libnet-config; then
         CFLAGS="$CFLAGS `${prefix}/bin/libnet-config --defines`"
     fi
     LNETINC="-I${prefix}/include"
     LNETINCDIR="$prefix/include/"
     LNETLIB="-L${prefix}/lib -lnet"
  elif test -f /usr/include/libnet.h; then
     if test -x /usr/bin/libnet-config; then
	     CFLAGS="$CFLAGS `/usr/bin/libnet-config --defines`" 
     fi
     LNETINCDIR="/usr/include/"
     LNETLIB="-lnet"
  else
     AC_MSG_RESULT(no)
     AC_ERROR(libnet not found)
  fi
  AC_MSG_RESULT(yes) ]
)
AC_SUBST(LNETINC)
AC_SUBST(LNETLIB)


dnl Checks to see what version of libnet we've got
OLDLIBS="$LIBS"
LIBS="$LNETLIB"

AC_MSG_CHECKING(for libnet version)

AC_ARG_WITH(libnetver,
[  --with-libnetver=VER    version of libnet in use],
[ case "$withval" in
  1.0.x)
     AC_MSG_RESULT($withval)
     libnet_ver_10=yes
     libnet_ver_11=no
     AC_DEFINE(USE_LIBNET_VERSION, 10)
     ;;
  1.1.x)
     AC_MSG_RESULT($withval)
     libnet_ver_10=no
     libnet_ver_11=yes
     AC_DEFINE(USE_LIBNET_VERSION, 11)
     ;;
  *)
     AC_MSG_ERROR(Invalid version: $withval
  valid versions are 1.0.x or 1.1.x)
     ;;
esac
], 
[ AC_TRY_RUN([
#include <string.h>
#include "$LNETINCDIR/libnet.h"
#define LIB_TEST "1.0"
/* 
 * simple proggy to test the version of libnet
 * returns zero if it's 1.0.x 
 * or one otherwise
 */
int
main (int argc, char *argv[])
{
	if (strncmp(LIB_TEST, LIBNET_VERSION, 3) == 0)
		exit(0);
	exit(1);
}],
	libnet_ver_10=yes
	AC_DEFINE(USE_LIBNET_VERSION, 10)
	AC_MSG_RESULT(1.0.x), 
	libnet_ver_10=no,
	libnet_ver_10=no
	)

AC_TRY_RUN([
#include <string.h>
#include "$LNETINCDIR/libnet.h"
#define LIB_TEST "1.1"
/* 
 * simple proggy to test the version of libnet
 * returns zero if it's 1.0.x 
 * or one otherwise
 */
int
main (int argc, char *argv[])
{
	if (strncmp(LIB_TEST, LIBNET_VERSION, 3) == 0)
		exit(0);
	exit(1);
}],
	libnet_ver_11=yes
	AC_DEFINE(USE_LIBNET_VERSION, 11)
	AC_MSG_RESULT(1.1.x), 
	libnet_ver_11=no,
	libnet_ver_11=no
	)
])

if test $libnet_ver_10 = no -a $libnet_ver_11 = no ; then
	AC_MSG_RESULT(unknown)
	AC_MSG_ERROR(Unable to determine version of libnet)
fi

dnl for now, we don't handle 1.1.x so we have to abort
dnl hopefully soon we'll handle either version

dnl if test $libnet_ver_11 = yes ; then
dnl	AC_MSG_ERROR(Libnet version 1.1.x isn't currently supported.
dnl  Please use 1.0.2a)
dnl fi
dnl restore LIBS
LIBS="$OLDLIBS"

AC_MSG_CHECKING( for 'make test' profile)
if test "$host" != "$build" ; then
	AC_MSG_WARN(Unable to do tests when cross-compiling)
fi

dnl Allows user to choose which nic to use for testing purposes
AC_ARG_WITH(testnic,
[  --with-testnic=NIC      Select which network card to use for testing],
[ nic1=$withval
nic2=$withval
AC_MSG_RESULT(Using --with-testnic=$withval)],
[
dnl these need to be dynamic based on OS
case $host in
	*-*-linux*)
	nic1=eth0
	nic2=eth0
	AC_MSG_RESULT(Linux OK)
	;;

	*-*-solaris*)
	nic1=hme0
	nic2=hme0
	AC_MSG_RESULT(Solaris OK)
	;;

	*-*-sunos*)
	nic1=hme0
	nic2=hme0
	AC_MSG_RESULT(SunOS OK)
	;;

	powerpc-apple-darwin*)
	nic1=en0
	nic2=en0
	AC_MSG_RESULT(Apple OSX)
	;;

	*)
	AC_MSG_RESULT([$host is unknown!  
	Please use --with-nictest to select an interface for 'make test'])
	;;
esac])


AC_ARG_WITH(testnic2,
[  --with-testnic2=NIC2    Select an optional 2nd network card to use for testing],
[ nic2=$withval ])

AC_MSG_NOTICE(Using $nic1 for 1st test network interface card)
AC_MSG_NOTICE(Using $nic2 for 2nd test network interface card)
AC_SUBST(nic1)
AC_SUBST(nic2)

AC_OUTPUT(Makefile)
AC_OUTPUT(test/Makefile)
AC_OUTPUT(tcpreplay.spec)
